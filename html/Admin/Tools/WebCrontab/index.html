<& /Admin/Elements/Header, Title => $title &>
<& /Elements/Tabs &>

<& /Elements/ListActions, actions => \@results &>

<style>
    .el span.label {
        display: block;
        text-align: left;
    }
    .elgrp div.el {
        display: inline-block;
        margin: 10px 5px;
        vertical-align: top;
    }
</style>

<div id="crontab-events">
    <& Elements/CollectionTable, headers => $table_headers, rows => $table_rows &>
</div>

<form action="<% $web_path %>/Admin/Tools/WebCrontab/index.html" method="post">
<&| /Widgets/TitleBox,title => 'Environment variables', rolledup=>0 &>
    <div id="crontab-env">
%       foreach my $env (@$available_env) {
            <& Elements/CrontabEnv, variable_name => $env, variable_value => $crontab->{'env'}->{$env}, enabled => int(exists($crontab->{'env'}->{$env})) &>
%       }
    </div>
    <& /Elements/Submit, Name => 'submit-env', Label => loc('Save Changes') &>
</&>
</form>

<%INIT>
use Data::Dumper qw(Dumper);
use RT::Extension::WebCrontab;

my $web_path = RT->Config->Get('WebPath') || "";
if ($web_path =~ /\/$/) {
    RT::Logger->warning("[RT::Extension::WebCrontab]: WebPath has trailing slash, it recommends to remove it");
    $web_path =~ s/\/$//;
}


my $title = loc('Crontab configuration');
unless ($session{'CurrentUser'}->HasRight( Object=> $RT::System, Right => 'SuperUser')) {
    Abort(loc('This feature is only available to system administrators'));
}

my @results;
my $available_env = RT::Extension::WebCrontab::get_available_env_vars();

if ($ARGS{'submit-env'}) {
    my %new_env = map { $_ => $ARGS{"env-" . lc $_ . "-value"} } 
        grep {$ARGS{"env-" . lc $_ . "-enabled"}} 
        @$available_env;

    my ($res, $msg) = RT::Extension::WebCrontab::save_env(\%new_env);

    if ($res) {
        push @results, loc('Crontab saved');
    } else {
        push @results, loc('ERROR: Cannot save crontab: [_1]', $msg);
    }

    MaybeRedirectForResults(
        Actions   => \@results,
        Path      => "/Admin/Tools/WebCrontab"
    );
}

my $crontab = RT::Extension::WebCrontab::load_crontab();

# Fill data for CollectionTable
my $table_rows = [];
my $table_headers = [loc('#'), loc('Expression'), loc('Action'), loc('Template'), loc('Comment'), loc('Status')];

my $i = 0;
while (defined($crontab->{'events'}->[$i])) {
    my $e = $crontab->{'events'}->[$i];
    my $id = $i + 1;  # one-based
    unless ($e->{'skip'}) {
        push @$table_rows, {
            'link' => '/Admin/Tools/WebCrontab/edit.html?id=' . $id,
            'cols' => [
                (map {{
                        'pre' => '<a href="/Admin/Tools/WebCrontab/edit.html?id=' . $id . '">', 
                        'v' => $_,
                        'post' => '</a>'
                }}  (
                        $id,
                        (
                            map { defined($e->{$_}) ? loc($e->{$_}) : '' } 
                                ('expression', 'action', 'template')
                        ),
                    )
                ),
                
                {
                    'v' => $e->{'comment'} // '',
                    'td_style' => 'width: 30%'
                },

                $e->{'active'} ? 'Enabled' : 'Disabled'
            ],
        };
    }
    $i++;
}
</%INIT>

<%ARGS>

</%ARGS>
