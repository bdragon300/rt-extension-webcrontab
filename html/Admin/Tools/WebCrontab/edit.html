<& /Admin/Elements/Header, Title => $title &>
<& /Elements/Tabs &>

<& /Elements/ListActions, actions => \@results &>

<style>
    .el span.label {
        display: block;
        text-align: left;
    }
    .elgrp div.el {
        display: inline-block;
        margin: 10px 5px;
        vertical-align: top;
    }
</style>

<form action="/Admin/Tools/WebCrontab/edit.html?id=<% $id %>&Create=<% $Create %>" method="post">
%   my $event_to_display = @results ? \%new_event : \%current_event;
    <& Elements/CrontabEvent, filling => $available_values, data => $event_to_display, old_data => \%current_event, captions => $classes &>
    <& /Elements/Submit, Name => 'submit', Label => loc('Save Changes') &>
%   if ($Create == 0) {
        <& /Elements/Submit, Name => 'remove', Label => loc('REMOVE'), OnClick => "return confirm('" . loc("Do you want to remove event?") . "');" &>
%   }
</form>

<%init>
use Data::Dumper qw{Dumper};

my $title = loc('Crontab event');
unless ($session{'CurrentUser'}->HasRight( Object=> $RT::System, Right => 'SuperUser')) {
    Abort(loc('This feature is only available to system administrators'));
}

my $available_values = RT::Extension::WebCrontab::get_available_values();
my $classes = RT::Extension::WebCrontab::get_classes();

my $crontab = RT::Extension::WebCrontab::load_crontab();
my $events = $crontab->{'events'};
undef $crontab;

my @results;
my %current_event = (active => 1);
my %new_event = ();
my %old_event = ();

$id += 0;
$Create += 0;
unless ($Create) {
    if ( ! defined($events->[$id])
        || $events->[$id]->{'skip'})
    {
        Abort(loc('ERROR: Invalid id'));
    }
}


unless ($Create) {
    %current_event = %{$events->[$id]};
    # delete not significant keys
    delete @current_event{qw/obj skip/};
    #delete keys with undef values, i.e. ignore crontool parameters without args
    delete @current_event{grep ! defined $current_event{$_}, keys %current_event}; 
}

if ($ARGS{'remove'}) {
    splice(@$events, $id, 1);

    my ($res, $msg) = RT::Extension::WebCrontab::save_events($events);
    if ($res) {
        push @results, loc('Crontab saved');
    } else {
        push @results, loc('ERROR: Cannot save crontab: [_1]', $msg);
    }

    MaybeRedirectForResults(
        Actions   => \@results,
        Path      => "/Admin/Tools/WebCrontab"
    );
}

if ($ARGS{'submit'}) {
    %new_event = map { $_ => $ARGS{$_} } 
        grep { $ARGS{$_} } 
        (@$classes, 'expression', 'comment');
    $new_event{'active'} = $ARGS{'active'} ? '1' : '0';
    %old_event = map { $_ => $ARGS{'old-' . $_} } 
        grep { $ARGS{'old-' . $_} } 
        (@$classes, 'expression', 'comment');
    $old_event{'active'} = $ARGS{'old-active'} ? '1' : '0';

    # Check expression syntax
    my $expr_regex = qr/^[^\s]+(\s+[^\s]+){4,5}$/i;
    my $simple_expr_regex = qr/^\@(reboot|yearly|annually|monthly|weekly|daily|midnight|hourly)$/i;
    my @fields_regex = (
        qr/^([1-5]?[0-9]|\*)([,\/\-]([1-5]?[0-9]|\*))*$/,
        qr/^(1?[0-9]|2[0-3]|\*)([,\/\-](1?([0-9]|2[0-3])|\*))*$/,
        qr/^\?$|^([1-9]|[1-2][0-9]|3[0-1]|\*)[LW]?([,\/\-](([1-9]|[1-2][0-9]|3[0-1])[LW]?|[LW]|\*))*$/,
        qr/^([1-9]|1[0-2]|jan|feb|mar|apr|may|jun|jul|aug|sep|oct|nov|dec|\*)([,\/\-]([1-9]|1[0-2]|jan|feb|mar|apr|may|jun|jul|aug|sep|oct|nov|dec|\*))*$/i,
        qr/^\?$|^([0-6]L?|sun|mon|tue|wed|thu|fri|sat|\*)([#,\/\-]([0-6]L?|sun|mon|tue|wed|thu|fri|sat|\*))*$/i,
        qr/^(19[7-9][0-9]|20[0-9][0-9]|\*)([,\/\-](19[7-9][0-9]|20[0-9][0-9]|\*))*$/, # Optional
    );
    my @mismatch = ();
    if ($new_event{'expression'} =~ $simple_expr_regex) {  # sufficient match
        @mismatch = ();
    } elsif ($new_event{'expression'} =~ $expr_regex) {
        @mismatch = grep { $_ !~ shift @fields_regex } split(/\s+/, $new_event{'expression'}, scalar(@fields_regex)); 
    } else {
        @mismatch = ($new_event{'expression'});  # Show whole expression in error msg
    }
    if (@mismatch) {
        push @results, loc("ERROR: Error in the expression at \"[_1]\"", $mismatch[0]);
    }

    # Make sure the task has not actually changed from the last time
    unless ($Create) {
        no warnings;  # using ~~
        if ( ! (%old_event ~~ %current_event)
            || (grep {$current_event{$_} ne $old_event{$_}} keys %current_event))
        {
            push @results, loc("WARNING: Event was changed in the crontab on background. Press Save Changes again to overwrite.");
        }
    }
    
    unless (@results) {
        $id = scalar(@$events) if ($Create);
        $events->[$id] = \%new_event;

        my ($res, $msg) = RT::Extension::WebCrontab::save_events($events);

        if ($res) {
            push @results, loc('Crontab saved');
        } else {
            push @results, loc('ERROR: Cannot save crontab: [_1]', $msg);
        }

        MaybeRedirectForResults(
            Actions   => \@results,
            Path      => "/Admin/Tools/WebCrontab"
        );
    }
}
</%init>

<%args>
$id => 0
$Create => 0
$Remove => 0
</%args>